# Specify the minimum version for CMake
cmake_minimum_required (VERSION 3.6.1)

# Project's name
project (Convpot)

# Require the compiler to support at least C++11
set (CMAKE_CXX_STANDARD 11)

# the file(GLOB...) allows for wildcard additions:
file (GLOB SOURCES "src/*.cpp")

# build sqlite library
add_subdirectory (sqlite)
include_directories (sqlite/include)

# find odbc library    
set (ODBC_LIB odbc32)
link_directories (odbc)                                       

# bring the headers into the project
include_directories (include)

# configure static libraries
set (CMAKE_FIND_LIBRARY_SUFFIXES ".a")
set (CMAKE_EXE_LINKER_FLAGS "-static")
set (BUILD_SHARED_LIBRARIES OFF)

# set additional files and folderes
set (RESOURCE_FOLDER "${CMAKE_CURRENT_SOURCE_DIR}/resources")
set (CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
set (CPACK_RESOURCE_FILE_README "${CMAKE_CURRENT_SOURCE_DIR}/README.md")

# build and include version.cpp from git tag
include (cmake/gitversion.cmake)

# build and link
add_executable (convpot ${SOURCES} ${VERSION_FILE})
target_link_libraries (convpot m sqlite3 ${ODBC_LIB})
 
# install files and folders
install (TARGETS convpot RUNTIME DESTINATION "bin")
install (FILES ${CPACK_RESOURCE_FILE_LICENSE} DESTINATION "doc")
install (FILES ${CPACK_RESOURCE_FILE_README} DESTINATION "doc")
install (FILES "${CMAKE_CURRENT_SOURCE_DIR}/CONTRIBUTORS.md" DESTINATION "doc")
install (FILES "${CMAKE_CURRENT_SOURCE_DIR}/CHANGELOG.md" DESTINATION "doc")
install (DIRECTORY "${PROJECT_SOURCE_DIR}/test/" DESTINATION "examples")

# configure INSTALL_PATH in convpot.bat
set (INSTALL_PATH "\%USERPROFILE\%\\AppData\\Local\\${PROJECT_NAME} ${GIT_VERSION_SHORT}")
configure_file (${RESOURCE_FOLDER}/convpot.bat.in
                ${CMAKE_BINARY_DIR}/convpot.bat)
install (FILES "${CMAKE_BINARY_DIR}/convpot.bat" DESTINATION ".")

# build a CPack driven installer package               
set (CPACK_GENERATOR "NSIS")
set (CPACK_MODULE_PATH ${RESOURCE_FOLDER})
set (CPACK_PACKAGE_VENDOR "Alexander Pohl")
set (CPACK_NSIS_INSTALL_ROOT "\$PROFILE\\\\AppData\\\\Local")
set (CPACK_PACKAGE_INSTALL_DIRECTORY "${PROJECT_NAME} ${GIT_VERSION_SHORT}")
set (CPACK_PACKAGE_ICON "${RESOURCE_FOLDER}\\\\convpot_installer.bmp")
set (CPACK_NSIS_DISPLAY_NAME "${CPACK_PACKAGE_INSTALL_DIRECTORY}")
set (CPACK_NSIS_MUI_ICON "${RESOURCE_FOLDER}/convpot.ico")
set (CPACK_NSIS_MUI_UNIICON "${RESOURCE_FOLDER}/convpot.ico")
set (CPACK_NSIS_HELP_LINK "https://github.com/ahpohl/convpot")
set (CPACK_NSIS_MENU_LINKS "convpot.bat" "Run Convpot"
                           "examples" "Examples"
                           "${CPACK_NSIS_HELP_LINK}" "GitHub page")
set (CPACK_NSIS_MODIFY_PATH ON)
set (CPACK_NSIS_DEFINES "RequestExecutionLevel user")
include (CPack)